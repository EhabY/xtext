apply plugin: "org.gradle.java-base"
/*
/*
 * Root project configuration that is reused by subprojects to apply the Xtend compiler.
 */
if (!hasProperty('JENKINS_URL')) {
	ext.JENKINS_URL = 'https://ci.eclipse.org/xtext'
}

// The repositories to query when constructing the Xtend compiler classpath
repositories {
	mavenCentral()
	maven {
		name 'xtend-bootstrap'
		url "$JENKINS_URL/job/xtend-bootstrap/lastStableBuild/artifact/build-result/maven-repository/"
	}
}

// The Xtend compiler version to use
def bootstrapXtendVersion = versions.xtext_bootstrap

configurations {
	xtendCompiler {
		description 'Bootstrap dependencies for the Xtend compiler'
		resolutionStrategy {
			eachDependency {
				if (requested.group == 'org.eclipse.xtext' || requested.group == 'org.eclipse.xtend')
					useVersion(bootstrapXtendVersion)
			}
		}
		exclude group: 'asm'
	}
}

dependencies {
	xtendCompiler "org.eclipse.xtend:org.eclipse.xtend.core:$bootstrapXtendVersion"
	xtendCompiler "org.eclipse.xtext:org.eclipse.xtext.java:$bootstrapXtendVersion"
	xtendCompiler "org.eclipse.xtext:org.eclipse.xtext.smap:$bootstrapXtendVersion"
	xtendCompiler platform("org.eclipse.xtext:xtext-dev-bom:${versions.xtext_bootstrap}")
}
